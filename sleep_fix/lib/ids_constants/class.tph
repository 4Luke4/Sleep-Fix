// Let's set a variable for each valid entry in CLASS.IDS
// You can now write ~%CLASS_FIGHTER%~ instead of 2

DEFINE_ACTION_MACRO "CLASS_IDS" BEGIN
	LOCAL_SET "read_class" = 0
	LOCAL_SET "i" = 0
	LOCAL_SPRINT "identifier" ""
	// The following variables are all set by `COPY_EXISTING`
	LOCAL_SPRINT "SOURCE_DIRECTORY" ""
	LOCAL_SPRINT "SOURCE_FILESPEC" ""
	LOCAL_SPRINT "SOURCE_FILE" ""
	LOCAL_SPRINT "SOURCE_RES" ""
	LOCAL_SPRINT "SOURCE_EXT" ""
	LOCAL_SET "SOURCE_SIZE" = 0
	LOCAL_SPRINT "DEST_DIRECTORY" ""
	LOCAL_SPRINT "DEST_FILESPEC" ""
	LOCAL_SPRINT "DEST_FILE" ""
	LOCAL_SPRINT "DEST_RES" ""
	LOCAL_SPRINT "DEST_EXT" ""
	// Main
	COPY_EXISTING - "CLASS.IDS" "override"
		READ_2DA_ENTRIES_NOW "read_class" 0
		FOR ("i" = 1; "%i%" < "%read_class%"; "i" += 1) BEGIN	// Skip 1st row ~IDS V1.0~
			READ_2DA_ENTRY_FORMER "read_class" "%i%" 1 "identifier"
			TO_UPPER "identifier"
			SET "CLASS_%identifier%" = IDS_OF_SYMBOL (~CLASS~ ~%identifier%~)
		END
	BUT_ONLY_IF_IT_CHANGES
END
