// Let's set a variable for each valid entry in ANIMATE.IDS
// You can now write ~%ANIMATE_DOPPLEGANGER_GREATER%~ instead of 112

DEFINE_ACTION_MACRO "ANIMATE_IDS" BEGIN
	LOCAL_SET "read_animate" = 0
	LOCAL_SET "i" = 0
	LOCAL_SPRINT "identifier" ""
	// The following variables are all set by `COPY_EXISTING`
	LOCAL_SPRINT "SOURCE_DIRECTORY" ""
	LOCAL_SPRINT "SOURCE_FILESPEC" ""
	LOCAL_SPRINT "SOURCE_FILE" ""
	LOCAL_SPRINT "SOURCE_RES" ""
	LOCAL_SPRINT "SOURCE_EXT" ""
	LOCAL_SET "SOURCE_SIZE" = 0
	LOCAL_SPRINT "DEST_DIRECTORY" ""
	LOCAL_SPRINT "DEST_FILESPEC" ""
	LOCAL_SPRINT "DEST_FILE" ""
	LOCAL_SPRINT "DEST_RES" ""
	LOCAL_SPRINT "DEST_EXT" ""
	// Main
	COPY_EXISTING - "ANIMATE.IDS" "override"
		READ_2DA_ENTRIES_NOW "read_animate" 0
		FOR ("i" = 1; "%i%" < "%read_animate%"; "i" += 1) BEGIN	// Skip 1st row ~IDS V1.0~
			READ_2DA_ENTRY_FORMER "read_animate" "%i%" 1 "identifier"
			TO_UPPER "identifier"
			SET "ANIMATE_%identifier%" = IDS_OF_SYMBOL (~ANIMATE~ ~%identifier%~)
		END
	BUT_ONLY_IF_IT_CHANGES
END